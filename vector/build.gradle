apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    defaultConfig {
        applicationId "im.vector"
        minSdkVersion 16
        targetSdkVersion 25
        // use the version code
        versionCode rootProject.ext.versionCodeProp
        versionName rootProject.ext.versionNameProp

        // custom values
        resValue "string", "bug_report_url",  "https://riot.im/bugreports/submit"
    }

    signingConfigs {
        release {
            storeFile file(project.hasProperty("RELEASE_STORE_FILE") ? RELEASE_STORE_FILE : "/dummy/path")
            storePassword project.hasProperty("RELEASE_STORE_PASSWORD") ? RELEASE_STORE_PASSWORD : "dummy password"
            keyAlias project.hasProperty("RELEASE_KEY_ALIAS") ? RELEASE_KEY_ALIAS : "dummy alias"
            keyPassword project.hasProperty("RELEASE_KEY_PASSWORD") ? RELEASE_KEY_PASSWORD : "dummy password"
        }
    }

    buildTypes {
        debug {
            resValue "string", "git_revision", "\"${gitRevision()}\""
            resValue "string", "git_revision_date", "\"${gitRevisionDate()}\""
            resValue "string", "git_branch_name", "\"${gitBranchName()}\""
            minifyEnabled false
        }

        release {
            resValue "string", "git_revision", "\"${gitRevision()}\""
            resValue "string", "git_revision_date", "\"${gitRevisionDate()}\""
            resValue "string", "git_branch_name", "\"${gitBranchName()}\""
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        matrixorg.initWith(buildTypes.release)
        matrixorg {
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        app {
            applicationId "im.vector.alpha"
            // use the version name
            versionCode rootProject.ext.versionCodeProp
            versionName rootProject.ext.versionNameProp
            resValue "string", "allow_gcm_use", "true"
            resValue "string", "allow_ga_use", "true"
            resValue "string", "flavor_description", "GooglePlay"
        }

        appfdroid {
            applicationId "im.vector.alpha"
            // use the version name
            versionCode rootProject.ext.versionCodeProp
            versionName rootProject.ext.versionNameProp
            resValue "string", "allow_gcm_use", "false"
            resValue "string", "allow_ga_use", "false"
            resValue "string", "flavor_description", "FDroid"
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    repositories {
        flatDir {
            dir 'libs'
        }

        maven {
            url "https://s3.amazonaws.com/repo.commonsware.com"
        }

        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
    }
}

def gitRevision() {
    def cmd = "git rev-parse --short HEAD"
    return cmd.execute().text.trim()
}

def gitRevisionDate() {
    def cmd = "git show -s --format=%ci HEAD^{commit}"
    return cmd.execute().text.trim()
}

def gitBranchName() {
    def cmd = "git name-rev --name-only HEAD"
    return cmd.execute().text.trim()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'me.leolin:ShortcutBadger:1.1.2@aar'

    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'

    compile 'com.squareup.retrofit:retrofit:1.6.1'
    compile 'com.google.code.gson:gson:2.3'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.2.0'
    compile 'com.squareup.okhttp:okhttp:2.2.0'
    compile 'io.pristine:libjingle:9690@aar'

    compile 'pl.droidsonroids.gif:android-gif-drawable:1.2.+'

    compile 'com.googlecode.libphonenumber:libphonenumber:8.0.1'

    /************* Matrix SDK management **************/
    // use the matrix SDK as external lib
    compile(name: 'matrix-sdk', ext: 'aar')
    compile(name: 'olm-sdk', ext: 'aar')

    // use the matrix SDK as a part of the project
    // you have to uncomment some lines in settings.gradle
    //compile project(':matrix-sdk')

    /************* flavors management **************/

    // app flavor only
    appCompile 'com.google.firebase:firebase-core:10.2.1'
    appCompile 'com.google.firebase:firebase-messaging:10.2.1'
    appCompile 'com.google.android.gms:play-services-analytics:10.2.1'

    // fdroid flavor only
}


if (!getGradle().getStartParameter().getTaskRequests().toString().contains("fdroid")) {
    apply plugin: 'com.google.gms.google-services'
}